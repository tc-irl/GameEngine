#version 410

in vec3 position_eye;
in vec3 normal_eye;
uniform samplerCube cSampler;

uniform mat4 view; // view matrix

// uniform float ratio;
uniform float ratioR;
uniform float ratioG;
uniform float ratioB;
uniform float reflectFactor;
out vec4 FragColor;

void main () 
{
	/* reflect ray around normal from eye to surface */
	vec3 incident_eye = normalize (position_eye);
	vec3 normal = normalize (normal_eye);

    vec3 refractedVecR = refract(incident_eye, normal, ratioR);
	refractedVecR = vec3 (inverse(view) * vec4(refractedVecR, 0.0));

	vec3 refractedVecG = refract(incident_eye, normal, ratioG);
	refractedVecG = vec3 (inverse(view) * vec4(refractedVecG, 0.0));

	vec3 refractedVecB = refract(incident_eye, normal, ratioB);
	refractedVecB = vec3 (inverse(view) * vec4(refractedVecB, 0.0));

	vec4 refractColor = vec4(0.0);

	refractColor.r = texture (cSampler, refractedVecR).r;
	refractColor.g = texture (cSampler, refractedVecG).g;
	refractColor.b = texture (cSampler, refractedVecB).b;
	refractColor.a = 1.0;

	vec3 reflected = reflect (incident_eye, normal);
	reflected = vec3 (inverse (view) * vec4 (reflected, 0.0));
    vec4 reflectColor = texture(cSampler, reflected);

	FragColor = mix(refractColor, reflectColor, reflectFactor);
}
